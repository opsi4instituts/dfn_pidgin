; Copyright (c) uib gmbh (www.uib.de)
; This sourcecode is owned by uib
; and published under the Terms of the General Public License.
; credits: http://www.opsi.org/credits/

[Actions]
; common Values for (un)installation
include_insert "common.opsiinc"

if not(HasMinimumSpace ("%SystemDrive%", $MinimumSpace$))
	LogError "Not enough space on %SystemDrive%, " + $MinimumSpace$ + " on drive %SystemDrive% needed for " + $ProductId$
	isFatalError
	; Stop process and set installation status to failed
else
	comment "Show product picture"
	ShowBitmap "%ScriptPath%\" + $ProductName$ + ".png" $ProductName$
	
	if FileExists("%ScriptPath%\delsub.opsiscript")
		comment "Start uninstall sub section"
		Sub "%ScriptPath%\delsub.opsiscript"
	endif
	
	Message "Installing " + $ProductName$ + " 32 Bit..."

	; wird als decimal erwartet, nicht hex: de (hex: 0407, dec: 1031), en_US (hex: 0409, dec: 1033)
	; https://msdn.microsoft.com/en-us/goglobal/bb895996.aspx
	DefVar $LCID$
	DefVar  $Language$
	Set     $Language$ = GetProductProperty("language","auto")

	if ($Language$ = "auto")
		comment "decide which language to use"
		DefStringList $LanguageMap$
		DefVar $System_Language$
		set $LanguageMap$ = GetLocaleInfoMap
		set $System_language$ = lower(getValue("system_default_language_id", $languagemap$))
		if $System_language$ = "0407"
        		comment "The detected language code is German"
        		Set $LCID$ = "1031"
        	endif
        	if $System_language$ = "0409"
        		comment "The detected language code is English"
        		Set $LCID$ = "1033"
        	endif
        endif

	if ($Language$ = "de")
        	Set $LCID$ = "1031"		
	endif
	if ($Language$ = "en")
		Set $LCID$ = "1033"
	endif

	comment "Start setup program"
	Winbatch_install
	Sub_check_exitcode
		
	Set $SearchPattern$ = $ProductName$
	; Parameter: $SearchPattern$ Suchbegriff in Registry
	Sub_search_registry32_uninstall_keys
	; Rückgabewert: $ResultList$ gefundene Einträge

	if ( count ($ResultList$) = "0" )
		logError "Fatal: After Installation " + $ProductName$ + " could not be found in " + $RegPathUninstall$
		isFatalError
	else
		comment "Successful Installation"
		comment "Get 'DisplayIcon' from registry for path to exe"
		Set $RegId$ = takeSTring(0, $ResultList$)	
		Set $PathToExe$ = GetRegistryStringValue32("[" + $RegPathUninstall$ + "\" + $RegId$ + "] DisplayIcon")
		
		set $DesktopIcon$ = getProductProperty("desktopicon","no")
		if $DesktopIcon$ = "yes"
			comment "Create common desktop icon"
			Linkfolder_install_desktopicon	
		endif

		comment "include custom post install file"
		set $CustomPostInstall$ = getProductProperty("custom-post-install","none")
		if not ($CustomPostInstall$ = "none")
			if FileExists("%ScriptPath%\custom\" + $CustomPostInstall$)
				include_insert "%ScriptPath%\custom\" + $CustomPostInstall$
			endif
		endif
	endif				
endif

[Winbatch_install]
"%ScriptPath%\$InstallProgram$" /DS=0 /L=$LCID$ /S

[Linkfolder_install_desktopicon]
set_basefolder common_desktopdirectory
set_subfolder ""

set_link
 	name: $ProductName$
 	target: $PathToExe$
end_link

