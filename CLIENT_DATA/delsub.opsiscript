Message "Searching installation of " + $ProductName$
; Ergebnis ist StringList (mehrere Einträge möglich);
; in einer Zeile werden drei Ergebnisse mit "|" abgetrennt geliefert: Registrykey (mit Pfad) | Bereich Registry (kann direkt getRegistryValue übergeben werden | DisplayName
; z.B. HKEY_LOCAL_MACHINE\SOFTWARE\Microsoft\Windows\CurrentVersion\Uninstall\Test64|64bit|Test Registry Entry64
Set $ResultList$ = SearchRegistryUninstallKeys($ProductName$)

Switch count ($ResultList$)
	Case "0"
		comment "No installations of " + $ProductName$ + " found in Registry. Nothing to do ;-)"
	EndCase
	Case "1"
		Message "Uninstalling (old) %installingProdName% 32Bit..."

		; notwendig, sonst werden einige Dateien nicht geloescht
		killtask "pidgin.exe"

		Set $RegId$ = takeString (0,splitString(takeString(0,$ResultList$), '|'))
		Set $RegBitScope$ = takeString (1,splitString(takeString(0,$ResultList$), '|'))
		Set $UninstallProgram$ = GetRegistryValue($RegId$, "UninstallString", $RegBitScope$)
		Set $InstallDir$ = ExtractFilePath($UninstallProgram$)

		Winbatch_uninstall
		Set $ExitCode$ = getLastExitCode
		if stringToBool(isNsisExitcodeFatal($ExitCode$, "True", $ErrorString$))
			isFatalError
		endif

		; notwendig, da uninstaller ein paar dateien/verzeichnisse vergisst
		comment "Delete files"
		Files_uninstall

		if $DesktopLink$ = "True"
			comment "Delete common desktop icon"
			LinkFolder_delete_desktoplink
		endif

		comment "include custom post deinstall file"
		set $CustomPostDeinstall$ = getProductProperty("custom-post-deinstall","none")
		if not ($CustomPostDeinstall$ = "none")
			if FileExists("%ScriptPath%\custom\" + $CustomPostDeinstall$)
				include_insert "%ScriptPath%\custom\" + $CustomPostDeinstall$
			endif
		endif
	EndCase
	DefaultCase
		logError "Found more than one installation! This case is not intended yet in this script! Stop now."
		isFatalError
	EndCase
EndSwitch

[Winbatch_uninstall]
"$UninstallProgram$" /S

[Files_uninstall]
delete -sf "$InstallDir$\"

[LinkFolder_delete_desktoplink]
set_basefolder common_desktopdirectory
set_subfolder ""
delete_element $ProductName$

